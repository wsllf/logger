buildscript {
  ext.kotlinVersion = '1.5.21'
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'checkstyle'

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  minSdkVersion = 9
  targetSdkVersion = 30
  compileSdkVersion = 30
  buildToolsVersion = '30.0.3'
}

ext.deps = [
    junit             : 'junit:junit:4.12',
    truth             : 'com.google.truth:truth:0.28',
    robolectric       : 'org.robolectric:robolectric:3.3',
    mockito           : "org.mockito:mockito-core:2.8.9",
    json              : "org.json:json:20160810",
    supportAnnotations: 'androidx.annotation:annotation:1.2.0',
    kotlin            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
]